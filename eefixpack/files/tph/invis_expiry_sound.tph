// since invis is usually broken before its (exceptionally long) duration, the expiry sound
// is often played even though the invis is long gone. so we push it out to an external
// spell and only apply if the creature is actually still invisible
COPY ~eefixpack/files/spl/cdinvexp.spl~ ~override~ // plays eff_m10

COPY_EXISTING ~bdnighte.spl~ ~override~ // invis via the night's gift bdleat07.itm [sod]
              ~ohbwi405.spl~ ~override~ // improved invisibility
              ~spin544.spl~  ~override~ // psionic superior invis
              ~spin687.spl~  ~override~ // create shadows
              ~spwi307.spl~  ~override~ // invis 10'
              ~spwi405.spl~  ~override~ // improved invis
              ~spwi505.spl~  ~override~ // shadow door
              ~spwi721.spl~  ~override~ // mass invis
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off + 0x1e + (index * 0x28)) abil_fx_num
    READ_SHORT (abil_off + 0x20 + (index * 0x28)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN 
      READ_SHORT (fx_off + 0x00 + (0x30 * (index2 + abil_fx_idx))) opcode
      READ_BYTE  (fx_off + 0x0c + (0x30 * (index2 + abil_fx_idx))) timing
      READ_ASCII (fx_off + 0x14 + (0x30 * (index2 + abil_fx_idx))) resref
      PATCH_IF ((opcode = 174) AND (timing = 4) AND
                ("%resref%" STRING_COMPARE_CASE "eff_m10" = 0)) BEGIN // expiry sound
        PATCH_PRINT "              ~%SOURCE_FILE%~  ~override~ //"          
        WRITE_SHORT (fx_off + 0x00 + (0x30 * (index2 + abil_fx_idx))) 326        // apply effect list
        WRITE_LONG  (fx_off + 0x08 + (0x30 * (index2 + abil_fx_idx))) 142        // if state invis or improved invis
        WRITE_LONG  (fx_off + 0x0e + (0x30 * (index2 + abil_fx_idx))) (THIS - 3) // three seconds before expiry
        WRITE_ASCII (fx_off + 0x14 + (0x30 * (index2 + abil_fx_idx))) cdinvexp   // expiry sound spell
      END
    END
  END
  BUT_ONLY IF_EXISTS 