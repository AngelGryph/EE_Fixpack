// Add workaround for buggy GlobalTimer() actions:
// https://www.gibberlings3.net/forums/topic/34892-1/?do=findComment&comment=320376

// Replace: SetGlobalTimer(name,scope,time) -> SetGlobalTimerRandom(name,scope,time,time)
COPY_EXISTING ~0500bedi.bcs~ ~override~
              ~0500ggrd.bcs~ ~override~
              ~0502gena.bcs~ ~override~
              ~0502gfcl.bcs~ ~override~
              ~0502guaa.bcs~ ~override~
              ~0503bedi.bcs~ ~override~
              ~0503ggrd.bcs~ ~override~
              ~0503keld.bcs~ ~override~
              ~0503rosa.bcs~ ~override~
              ~0503sars.bcs~ ~override~
              ~0503smth.bcs~ ~override~
              ~0503work.bcs~ ~override~
              ~0503xand.bcs~ ~override~
              ~0511ing1.bcs~ ~override~
              ~0511ing2.bcs~ ~override~
              ~0512grd1.bcs~ ~override~
              ~0512grd2.bcs~ ~override~
              ~0512grd3.bcs~ ~override~
              ~0512grd4.bcs~ ~override~
              ~0512wkat.bcs~ ~override~
              ~0600dilg.bcs~ ~override~
              ~0600ral1.bcs~ ~override~
              ~0602sen8.bcs~ ~override~
              ~0604sen4.bcs~ ~override~
              ~0612pat1.bcs~ ~override~
              ~1600t1ld.bcs~ ~override~
              ~1600t2ld.bcs~ ~override~
              ~1600t3ld.bcs~ ~override~
              ~1600t4ld.bcs~ ~override~
              ~1600t5ld.bcs~ ~override~
              ~1600t6ld.bcs~ ~override~
              ~1600t7ld.bcs~ ~override~
              ~ar0500.bcs~ ~override~
              ~ar0502.bcs~ ~override~
              ~ar0503.bcs~ ~override~
              ~ar0511.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~SetGlobalTimer("\([^"]+\)","\([^"]+\)",\([^)]+\))~
      ~SetGlobalTimerRandom("\1","\2",\3,\3)~
  END
BUT_ONLY

// Dialogue actions require a different approach, as using the same timer value
// for min and max results in a random timer between 0 and max.
COPY_EXISTING ~dgaoha.dlg~ ~override~
              ~dgspirit.dlg~ ~override~
              ~dlenny.dlg~ ~override~
              ~dsabast.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    // SetGlobalTimer(name,scope,time) -> SetGlobalTimerRandom(name,scope,time,time+1)
    REPLACE_EVALUATE
    ~SetGlobalTimer("\([^"]+\)","\([^"]+\)",\([^)]+\))~
    BEGIN
      PATCH_IF (IS_AN_INT ~MATCH3~) BEGIN
        SET MATCH4 = MATCH3 + 1
      END ELSE BEGIN
        SET MATCH4 = IDS_OF_SYMBOL(~gtimes~ ~%MATCH3%~) + 1
      END
    END
    ~SetGlobalTimerRandom("%MATCH1%","%MATCH2%",%MATCH3%,%MATCH4%)~
  END
BUT_ONLY


// Replace instances of Attack() -> RunningAttack() in combat scripts

// Controls which "Attack()" instance should be updated
ACTION_DEFINE_ASSOCIATIVE_ARRAY pst_replace_action BEGIN
  // script resref => space-separated list of 0-based instance indices
  ~0402tegr~ => ~0~
  ~0500fhgd~ => ~2~
  ~0500mhgd~ => ~2~
END

COPY_EXISTING ~0000bxd2.bcs~ ~override~
              ~0000hgrd.bcs~ ~override~
              ~0000hsrg.bcs~ ~override~
              ~0209skel.bcs~ ~override~
              ~0209skld.bcs~ ~override~
              ~0402tegr.bcs~ ~override~
              // ~0403blnd.bcs~ ~override~  // Tiresias (Blind Thug): should not run
              ~0403drnk.bcs~ ~override~
              ~0403gang.bcs~ ~override~
              ~0403gdul.bcs~ ~override~
              ~0403ggng.bcs~ ~override~
              ~0403gngb.bcs~ ~override~
              ~0403hivr.bcs~ ~override~
              ~0403mage.bcs~ ~override~
              ~0403pain.bcs~ ~override~
              ~0403prop.bcs~ ~override~
              ~0403sent.bcs~ ~override~
              ~0403thg1.bcs~ ~override~
              ~0403thg2.bcs~ ~override~
              ~0500fhgd.bcs~ ~override~
              ~0500mhgd.bcs~ ~override~
              ~0700atta.bcs~ ~override~
              ~0701atta.bcs~ ~override~
              ~0701hgrd.bcs~ ~override~
              // ~0702cass.bcs~ ~override~  // Cassius: Nupperibo appearance, should not run
              ~0702gar2.bcs~ ~override~
              ~0702gar3.bcs~ ~override~
              ~0702gard.bcs~ ~override~
              ~0702prsn.bcs~ ~override~
              ~0702town.bcs~ ~override~
              ~0702tria.bcs~ ~override~
              ~0708abas.bcs~ ~override~
              ~0708camp.bcs~ ~override~
              ~0708corn.bcs~ ~override~
              ~0708ghri.bcs~ ~override~
              // ~0708lemu.bcs~ ~override~  // Lemure: should not run
              // ~0708nupp.bcs~ ~override~  // Nupperibo: should not run
              ~0708tre0.bcs~ ~override~
              ~0708tre1.bcs~ ~override~
              ~0708tre2.bcs~ ~override~
              ~0900avng.bcs~ ~override~
              // ~0900ctkr.bcs~ ~override~  // Dump Caretaker in Curst: Old man, should not run
              // ~0900gnr1.bcs~ ~override~  // handled elsewhere
              // ~0900gnr2.bcs~ ~override~  // handled elsewhere
              ~0900grdg.bcs~ ~override~
              // ~0900hrmt.bcs~ ~override~  // Hermit: should not run
              ~0900hrs1.bcs~ ~override~
              ~0900hrs2.bcs~ ~override~
              ~0900hrs3.bcs~ ~override~
              ~0900jjog.bcs~ ~override~
              ~0900loot.bcs~ ~override~
              ~0900mob.bcs~ ~override~
              // ~0900slv0.bcs~ ~override~  // Thokola: should not run
              ~0900tem1.bcs~ ~override~
              ~0900tem2.bcs~ ~override~
              ~0900tem3.bcs~ ~override~
              ~0900tem4.bcs~ ~override~
              // ~0900tgrd.bcs~ ~override~  // Thokola: should not run
              ~0900thgb.bcs~ ~override~
              ~0901geh6.bcs~ ~override~
              ~0901gehr.bcs~ ~override~
              ~0901grds.bcs~ ~override~
              ~0901loot.bcs~ ~override~
              ~0902ana0.bcs~ ~override~
              ~0902ana1.bcs~ ~override~
              ~0902ana2.bcs~ ~override~
              ~0902ana5.bcs~ ~override~
              ~0902ana6.bcs~ ~override~
              ~0902ebbb.bcs~ ~override~
              ~0903thg.bcs~ ~override~
              ~0904guy.bcs~ ~override~
              ~1000abd2.bcs~ ~override~
              ~1000abid.bcs~ ~override~
              ~1000corb.bcs~ ~override~
              ~1000corc.bcs~ ~override~
              ~1000cut3.bcs~ ~override~
              // ~1000fnds.bcs~ ~override~  // handled elsewhere
              // ~1000lemd.bcs~ ~override~  // Lemure: should not run
              ~1100anim.bcs~ ~override~
              ~1100grg0.bcs~ ~override~
              ~1100grg4.bcs~ ~override~
              // ~1100grk0.bcs~ ~override~  // Gronk: should not run
              ~1101atta.bcs~ ~override~
              ~1101cut3.bcs~ ~override~
              ~1101cut4.bcs~ ~override~
              ~1101fhju.bcs~ ~override~
              // ~1201sha2.bcs~ ~override~  // Greater Shadow: should not run
              // ~1201shad.bcs~ ~override~  // Greater Shadow: should not run
              ~1202vhai.bcs~ ~override~
              ~1203inc.bcs~ ~override~
              ~1203inc1.bcs~ ~override~
              ~1203inc2.bcs~ ~override~
              ~1203inc3.bcs~ ~override~
              // ~1300cons.bcs~ ~override~  // Low/Medium/High Threat Constructs: should not run
              // ~2000cati.bcs~ ~override~  // handled elsewhere
              ~3015grd1.bcs~ ~override~
              ~3015grd2.bcs~ ~override~
              ~3015sohm.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    // preparing instances for find-and-replace
    TEXT_SPRINT resref ~%SOURCE_RES%~
    TO_LOWER ~resref~
    PATCH_IF (VARIABLE_IS_SET $pst_replace_action(~%resref%~)) BEGIN
      TEXT_SPRINT indices $pst_replace_action(~%resref%~)
    END ELSE BEGIN
      TEXT_SPRINT indices ~~
    END

    // Find-and-replace for selected instances: Attack -> RunningAttack
    SET index = 0
    REPLACE_EVALUATE CASE_SENSITIVE
      ~\bAttack(~
      BEGIN
        PATCH_IF (~%indices%~ STR_EQ ~~ OR INDEX(~\b%index%\b~ ~%indices%~) >= 0) BEGIN
          TEXT_SPRINT action ~RunningAttack~
        END ELSE BEGIN
          TEXT_SPRINT action ~Attack~
        END
        SET index += 1
      END
      ~%action%(~
  END
BUT_ONLY


// TTO's voiced line "TRA011.WAV" should not be cut off by his following voiced line
COPY_EXISTING ~1201csg8.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~WaitSync(1)~ ~WaitSync(3)~
  END
BUT_ONLY


// Fix for Nordom sometimes not "dying" in the TTO/Nordom cutscene in the Fortress of Regrets
// Note: Issue is possibly a side effect of adding ClearAllActions() to DMACH3.DLG (see ../d/pstee_core_fixes.d)
COPY_EXISTING ~1201csn8.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Attack("Trans")~ ~AttackOneRound("Trans")~
  END
BUT_ONLY
